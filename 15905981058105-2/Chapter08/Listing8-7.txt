Imports System
Imports System.Data
Imports System.Data.SqlClient

Module Module1

   Sub Main()
      ' Set up connection string
      Dim connString As String = _
           "server = .\sqlexpress;" _
         & "integrated security = true;" _
         & "database = northwind"

      ' Set up query
      Dim qry As String = _
           "select      " _
         & "   * " _
         & "from        " _
         & "   employees " _
         & "where " _
         & "   country = 'UK' "

      ' Set up DML
      Dim del As String = _
           "delete from employees " _
         & "where " _
         & "   employeeid = @employeeid "


      ' Create connection
      Dim conn As SqlConnection = New SqlConnection(connString)

      Try
         ' Create data adapter
         Dim da As SqlDataAdapter = New SqlDataAdapter(qry, conn)

         ' Create and fill dataset
         Dim ds As DataSet = New DataSet()
         da.Fill(ds, "employees")

         ' Get the data table reference
         Dim dt As DataTable = ds.Tables("employees")


         ' Delete Employees
         '
         ' Create command
         Dim cmd As SqlCommand = New SqlCommand(del, conn)
         '
         ' Map parameters
         '
         cmd.Parameters.Add( _
            "@employeeid", _
            SqlDbType.Int, _
            4, _
            "employeeid")
         '
         '
         ' Select employee to delete
         Dim filt As String = _
              "firstname = 'Roy' " _
            & "and " _
            & "lastname = 'Beatty' "
         '
         ' Delete employee from data table
         For Each row As DataRow In dt.Select(filt)
            row.Delete()
         Next
         '
         ' Update database
         da.DeleteCommand = cmd
         da.Update(ds, "employees")

         ' Display data table
         For Each row As DataRow In dt.Rows
            Console.WriteLine( _
               "{0} {1} {2}", _
               row("firstname").ToString().PadRight(15), _
               row("lastname").ToString().PadLeft(25), _
               row("city") _
            )
         Next
      Catch e As Exception
         ' Display error
         Console.WriteLine("Error: " & e.ToString)
      Finally
         ' Close connection
         conn.Close()
      End Try
   End Sub
End Module
