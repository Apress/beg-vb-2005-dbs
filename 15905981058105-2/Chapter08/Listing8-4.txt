Imports System
Imports System.Data
Imports System.Data.SqlClient

Module Module1

   Sub Main()
      ' Set up connection string
      Dim connString As String = _
           "server = .\sqlexpress;" _
         & "integrated security = true;" _
         & "database = northwind"

      ' Set up query
      Dim sql As String = _
           "select      " _
         & "   * " _
         & "from        " _
         & "   employees " _
         & "where " _
         & "   country = 'UK' "

      ' Create connection
      Dim conn As SqlConnection = New SqlConnection(connString)

      Try
         ' Create data adapter
         Dim da As SqlDataAdapter = New SqlDataAdapter(sql, conn)

         ' Create and fill dataset
         Dim ds As DataSet = New DataSet()
         da.Fill(ds, "employees")

         ' Get the data table reference
         Dim dt As DataTable = ds.Tables("employees")

         ' FirstName column should be nullable
         dt.Columns("firstname").AllowDBNull = True

         ' Modify City in first row
         dt.Rows(0)("city") = "Wilmington"

         ' Add a row
         Dim newRow As DataRow = dt.NewRow()
         newRow("firstname") = "Roy"
         newRow("lastname") = "Beatty"
         newRow("titleofcourtesy") = "Sir"
         newRow("city") = "Birmingham"
         newRow("country") = "UK"
         dt.Rows.Add(newRow)


         ' Display rows in data table
         For Each row As DataRow In dt.Rows
            Console.WriteLine( _
               "{0} {1} {2}", _
               row("firstname").ToString().PadRight(15), _
               row("lastname").ToString().PadLeft(25), _
               row("city"))
         Next

         '
         ' Code for updating the database would come here
         '

      Catch e As Exception
         ' Display error
         Console.WriteLine("Error: " & e.ToString)
      Finally
         ' Close connection
         conn.Close()
      End Try
   End Sub
End Module
