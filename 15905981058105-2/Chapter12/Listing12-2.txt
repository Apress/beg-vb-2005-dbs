Imports System
Imports System.Data
Imports System.Data.SqlClient

Module Module1

   Sub Main()
      ' Set up connection string
      Dim connString As String = _
           "server = .\sqlexpress;" _
         & "integrated security = true;" _
         & "database = northwind"

      ' Create connection
      Dim conn As SqlConnection = New SqlConnection(connString)

      Try
         ' Open connection
         conn.Open()

         ' Create command
         Dim cmd As SqlCommand = conn.CreateCommand()

         ' Specify stored procedure to execute
         cmd.CommandType = CommandType.StoredProcedure
         cmd.CommandText = "sp_orders_by_employeeid2"

         ' Create input parameter
         Dim inparm As SqlParameter = cmd.Parameters.Add( _
               "@employeeid", _
               SqlDbType.Int _
            )
         inparm.Direction = ParameterDirection.Input
         inparm.Value = 2

         ' Create output parameter
         Dim ouparm As SqlParameter = cmd.Parameters.Add( _
               "@ordercount", _
               SqlDbType.Int _
            )
         ouparm.Direction = ParameterDirection.Output

         ' Create return value parameter
         Dim retval As SqlParameter = cmd.Parameters.Add( _
               "return_value", _
               SqlDbType.Int _
            )
         retval.Direction = ParameterDirection.ReturnValue

         ' Execute command
         Dim rdr As SqlDataReader = cmd.ExecuteReader()

         ' Loop through result set
         While rdr.Read
            Console.WriteLine( _
               "{0} {1}", _
               rdr(0).ToString().PadRight(5), _
               rdr(1).ToString() _
            )
         End While

         ' Close data reader
         rdr.Close()

         ' Display output parameter
         Console.WriteLine( _
            "The output parameter value is {0}", _
            cmd.Parameters("@ordercount").Value _
            )

         ' Display return value
         Console.WriteLine( _
            "The return value is {0}", _
            cmd.Parameters("return_value").Value _
            )
      Catch e As Exception
         ' Display error
         Console.WriteLine("Error: " & e.ToString)
      Finally
         ' Close connection
         conn.Close()
      End Try
   End Sub
End Module
